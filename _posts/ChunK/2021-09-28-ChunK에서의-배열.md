---
title:  "[ChunK] 배열"

categories:
  - ChunK
tags:
  - [ChunK, Music, array]

toc: false
toc_sticky: true

---

> 이 포스트는 한양대학교 음악프로그래밍(CSE2020) 도경구 교수님 수업을 참조하였습니다. 개인적으로 공부하면서 참조하려고 만든 포스트입니다.


## 4. 배열

### 4-1 배열의 선언과 지정
`배열(array)`은 같은 타입의 값들을 나란히 이어붙여 나열된 데이터 구조이다.
음을 차례로 쉼표로 구분하고 전체를 대괄호로 둘러싼다.
```java
<<< [67,67,69,69,67,67,64] >>>;
```
위 코드를 실행 시키면 다음과 같이 실행창에 나타난다.

`The value of [67,67,69,69,67,67,64] = 0x600003a6df80`

배열이 저장되어 있는 주소를 프린트 해준 것이다.


#### 배열의 선언

배열을 활용하기 위해서는 변수로 지정하고 인덱스를 활용하여 개별 접근해야 한다. 배열 변수는 다음과 같은 형식으로 선언한다.
`int notes[7];`

선언할 배열 원소의 타입을 앞에 명시하고, 이어서 배열 변수의 이름과 배열의 크기를 대괄호로 둘러싸 작성한다.
원소의 값은 타입의 기본값으로 설정된다. int 타입은 0, float 타입은 0.0,string 타입은 "" 로 자동으로 설정된다.

#### 배열의 지정

배열은 다음과 같은 형식으로 한꺼번에 지정할 수 있다.
@ 기호를 반드시 사용해서 선언과 동시에 지정해 줄 수 있다.
이 때는 배열의 크기는 당연히 원소의 개수이므로 크기를 굳이 명시할 필요는 없다.
```java
[67,67,67,69,69,67,67,64] @=> notes;
```

#### 배열 보조 함수

1. 배열 길이 계산 함수 : `int size()`
2. 배열 길이 조정 함수 : `int size(int n)`
   이 함수를 활용하면 배열의 길이를 줄이거나 늘릴 수 있다.
   ChunK은 자유롭게 배열의 길이를 줄였다 늘렸다 할 수 있다.
3. 배열 뒤에서 원소 하나 제거하기 : `void popBack()`
   Back 에서 B 가 대문자 임에 주의하자.
4. 배열 뒤에 원소 하나 추가하기 : `void <<`
5. 배열 지우기 : `void clear()`

### 4-2 배열 활용 사례 : 학교종


```java
60 => int C4;
62 => int D4;
64 => int E4;
65 => int F4;
67 => int G4;
69 => int A4;
71 => int B4;
72 => int C5;
48 => int C3;
53 => int F3;
55 => int G3;
//major codes
// C : 60,64,67
// F : 65,69,72
// G7 : 67,71,74,77


[
G4,G4,A4,A4,G4,G4,E4,
G4,G4,E4,E4,D4,-1,
G4,G4,A4,A4,G4,G4,E4,
G4,E4,D4,E4,C4,-1
] @=> int NOTES[];


//base for school bells

[
C3,C3,F3,F3,C3,C3,C3,
C3,C3,C3,C3,G3,-1,
C3,C3,F3,F3,C3,C3,C3,
C3,C3,G3,G3,C3,-1
] @=> int BASS[];

// note length
0.5::second => dur beat;
beat / 4 => dur REST;
beat - REST => dur QN;  // quarter note
beat * 2 - REST => dur HN;  // half note
beat * 3 - REST => dur DHN; //dotted half note

//tempos for school bells;
[
QN,QN,QN,QN,QN,QN,HN,
QN,QN,QN,QN,DHN,QN,
QN,QN,QN,QN,QN,QN,HN,
QN,QN,QN,QN,DHN,QN
] @=> dur DURS[];
SinOsc s => dac;
TriOsc t => dac;

// play
for(0 => int i; i<NOTES.size(); i++){
    setOsc(s,NOTES[i],0.5);
    setOsc(t,BASS[i],0.1);
    DURS[i] => now;
    0 => s.gain;
    REST => now;
}



fun void setOsc(Osc osc,int note,float vol){
    if(note == -1){
        0 => osc.gain;
    }
    else{
        Std.mtof(note) => osc.freq;
        vol => osc.gain;
        
    }
    
}
```


## Summary
- 좀 더 프로그래밍적으로 음악을 만들 수 있게 되었다.
***


🌜 주관적인 견해가 담긴 글입니다. 다양한 의견이 있으실 경우
언제든지 댓글 혹은 메일로 지적해주시면 감사하겠습니다! 😄

