---
title:  "[ChunK] 소리내기 프로그래밍"

categories:
  - ChunK
tags:
  - [ChunK, Music,소리내기]

toc: true
toc_sticky: true

date: 2021-09-09
last_modified_at: 2021-09-09
---

# 소리내기 프로그래밍 기본
이 포스트는 한양대학교 음악프로그래밍(CSE2020) 도경구 교수님 수업을 참조하였습니다.
개인적으로 공부하면서 참조하려고 만든 포스트입니다.
ChunK 은 소리를 내는데 중점을 낸 프로그래밍 언어이고 기존의 다른 프로그래밍 언어와 비슷하다. 기본적으로 프로그래밍을 알고 있다고 가정한다. 소리를 내는 것을 중심으로 ChucK 이라는 언어에 대해서 배울 것이다.
## 변수와 타입
### 2개의 주파수로 이어서 소리 내기
주파수가 높으면 우리에게 약간 크게 들리는 경향이 있다.

``` java
SinOsc s => dac;    // dac에 Sin Osillation s 장착
220 => s.freq;      // s의 frequency 를 220 으로 설정
0.5::second => now; // 지금부터 0.5 동안 재생
440 => s.freq;      // s의 frequency 를 440 으로 설정
0.5::second => now; // 지금부터 0.5 동안 재생
```

### 무한루프
한 구간을 계속 반복해서 소리내게 하고 싶은 경우 사용한다.

``` java
SinOsc s => dac;
  
while(true) {
    220 => s.freq;
    0.5::second => now;
    440 => s.freq;
    0.5::second => now;
}
```
### 변수 선언 및 사용 
일반적인 프로그래밍과 비슷하지만 순서가 반대인 점을 주의해야 한다.

- 변수 선언
  문법 : <타입> <변수>
  예: int myPitch;,  float myGain;
  정수는 셀 수 있는 수 실수는 무한대로 있고 셀 수 없기때문에 메모리에 저장할 떄는 구별해서 저장한다.
  
- 할당(assignment)
  선언시에도 가능하고, 선언과 따로 할당도 가능하다.
  문법 : <식> => <변수>
  예 : 220 => myPitch; , 0.6 => myGain;
  
- 선언과 할당 
  문법 : <식> => <타입> <변수>
  예 : 220 => int myPitch;  0.6 => int myGain;



``` java
SinOsc s => dac;
220 => int myPitch; // myPitch 라는 int 변수에 220 할당(선언과 동시에 할당)
0.6 => float myGain; // myGain 이라는 float 변수에 0.6 할당
myGain => s.gain; // 0.6 을 갖고있는 myGain 을 s.gain값으로 설정(크기)
while(true){
  myPitch => s.freq;
  0.5::second => now;
  myPitch * 2 => s.freq;
  0.5::second => now;
}
```

### 소리사이 공백 넣기
소리가 난다 안난다로 차이가 난다. 소리를 낼때의 볼륨과 소리를 음소거했을때의 볼륨을 차이를 두면 된다.
한 음이 0.5 초를 발생시키지만 0.4초는 소리를키고 0.1초는 소리를 안내기때문에 소리가 구별이 된다.
소리를 다음 소리와 구별하기 위해서 이런 트릭을 사용할 수 있다.

```java
SinOsc s => dac;
220 => int myPitch;
0.6 => float onGain;
0.0 => float offGain; // gain 을 0.0 으로 설정하면 아무소리가 안들림 freq을 0.0으로설정해도같다

while(true)
{
  onGain => s.gain;     // 소리킴
  myPitch => s.freq;
  0.4::second => now;   // 0.4초동안 myPitch 주파수로 소리나옴
  offGain => s.gain;    // 소리끔
  0.1::second => now;   // 0.1초동안 공백(소리안남)
}
```

### 소리크기 조금씩 증가
볼륨컨트롤 소리의 크기가 0.0 에서 한번 loop 을 돌 떄마다 소리의 크기를 0.001 씩 증가시켜준다.
gain 은 보통 0.0 에서 1.0 으로 설정하는 것이 귀에 편하다. gain 을 2, 3 으로 이어폰을 통해 들으면 귀에 피해를 입을 수 있다.


``` java
SinOsc s => dac;
220 => int myPitch;
0.0 => float myGain;

while(true)    
{
    myGain => s.gain;
    0.09::second => now;
    0.001 +=> myGain;
    <<< myGain >>>;
}

```

### 키워드 now

- ChucK 의 기준 시계를 나타내는 특수 키워드
- ChunK 현재 시각 값을 항상 갖고 있음
- 프로그램에서 수정 불가
- 지금 VM 이 돌아가는 이 세계에서 딱 **'지금'** 을 나타낸다. 이 값은 변경이 안된다.


| 키워드 |          지속시간          |
| :----: | :------------------------: |
|  samp  | 1 디지털샘플 = 1/44100(초) |
|   ms   |         1/1000(초)         |
| second |           1(초)            |
| minute |           1(분)            |
|  hour  |          1(시간)           |
|  day   |           1(일)            |
|  week  |           1(주)            |



### ChucK 데이터 타입 
**dur**은 시간의 기간을 나타내고 **time** 은 딱 point 를 나타낸다 

| 데이터 타입 |       설명        |          예          |
| :---------: | :---------------: | :------------------: |
|     int     |   정수(Integer)   |        0,220         |
|    float    | 실수(Float point) |  0.6,3.12414,440.0   |
|   string    |      문자열       | "Hello","sound.wav"  |
|     dur     |       기간        | 1::second , 3*minute |
|    time     |    ChucK 시간     |       34580.0        |
|    void     |       없음        |                      |

3*minute 이나 3::minute 이나 3*1::minute 이나 세개는 다 똑같은 의미이다.
void 는 함수의 return 값이 아무것도 없을 경우 사용한다.

#### dur 타입 활용 사례
``` java
0.8::second => dur quarter;  // 1/4 
4::quarter => dur whole;      // 1
4 * quarter => dur whole;     // 1
whole / 2 => dur half;        // 1/2
quarter / 2 => dur eighth;    // 1/8

```


## 제어 구조

### 무작위수 자동 생성(Random Number Generation)


| METHOD                              | OUTPUT | DESCRIPTION                                                  |
| ----------------------------------- | ------ | ------------------------------------------------------------ |
| Math.random()                       | int    | generates random integer ben 0 and MATH.RANDOM_MAX           |
| Math.random2(int min, int max)      | int    | generates random integer in the range [min,max]              |
| Math.randomf()                      | float  | generates random floating point number in the range [0,1]    |
| Math.random2f(float min, float max) | float  | generates random floating point number in the range [min,max] |

```java
TriOsc t => dac;
while(true){
    Math.random2(30,1000) => t.freq;
    Math.random2f(0.0,1.0) => t.gain;
    Math.random2f(30,400)::ms => now;
}
```

### 조건문 if

평소에 알고 있는 다양한 프로그래밍언어와 비슷한 문법 구조이다.

```java
// sound network
SinOsc s => dac;

// set frequency
222.45 => s.freq;
// set volume
0.2 => s.gain;

// chance variable
3 => int chance;

if( chance == 1 )
{
  // sound only plays if chance == 1
  1::second => now;
}
else
{
  // set new frequency
  440.32 => s.freq;
  // play for 3 seconds 
  3::second => now;
}


```


### For statement


```java
for( 0 => int i; i<4; i++)
{
  <<< i >>>;
  1::second => now;
}

```
```java

SinOsc s => dac; // sound network

// For loop
for(20 => int i ; i< 400 ; i++)
{
  <<< i >>>;
  i => s.freq;
  .01::second => now;
}

```

For loop 을 while loop 으로 변환할 수 있다. 범위가 정해져있는 경우에는 For statement 를 사용하는것이 더 선호된다.

### while statement


```java
// sound network
SinOsc s => dac;

// initialize variable i 
20 => int i;

while( i < 400 ) 
{
  i => s.freq;
  <<< i >>>;
  0.01::second => now;
  // update in loop
  i++;
}

```



## 다른소리 석기(mixing)
- 두개 이상의 발진기로 소리를 같이 내게 할 수 있다.
- 소리크기는 다 합쳐서 1이 넘지 않게 하는 것이 적절하다.

### 발진기 2대 소리 같이 내기

```java
SinOsc s => dac;
220 => s.freq;
0.5 => s.gain;
SinOsc s2 => dac;
660 => s2.freq;
0.5 => s2.gain;
second => now;    
```


### 발진기 3대 소리 같이 내기

```java


SinOsc s => dac;
220 => s.freq;
0.333 => s.gain;
SinOsc s2 => dac;
330 => s2.freq;
0.333 => s2.gain;
SinOsc s3 => dac;
640 => s3.freq;
0.333 => s3.gain;
second => now;    
```



## ChucK 표준 라이브러리 Std
중요하다. Std library 중에 MIDI 이다
Musical Instrument Digital Interface
지금까지는 주파수를 설정해서 소리를 냈지만, 항상 주파수를 외우기는 힘들기 때문에 음악의 음을 주파수로 바꿔주는 라이브러리다.
음번호는 0~127까지 표준으로 정해져있다. 피아노건반에서 하나씩 올라갈때마다 한 숫자씩 올라간다. 피아노 건반의 가장 중간 **도** 가 60 이다.
라이브러리를 사용하면 언제든지 MIDI Number와 주파수를 변환시킬 수 있다. 

<br>
![image](https://user-images.githubusercontent.com/69495129/132610560-d17ad644-fa90-4bb6-a6cc-94619e4222fa.png)
<br>

|       함수 이름과 인수       |               의미                |
| :--------------------------: | :-------------------------------: |
|   float mtof(float midi);    | MIDI 번호를 주파수(Hz)로 변환해줌 |
| float ftom(float frequency); | 주파수(Hz)를 MIDI 번호로 변환해줌 |

### MIDI 스케일 듣기
가청 범위는 사람마다 다를 수 있다.

```java
SinOsc s => dac;
0.1 => s.gain;
float hz;
for(0 => int m; m < 128; m++){
    Std.mtof(m) => hz;
    <<< m, hz >>>;
    hz => s.freq;
    300::ms => now;
}
```

### 무작위로 MIDI 소리 만들기
소리의 크기도, 소리의 주파수도 랜덤으로 뽑아진다. 이떄 소리의 크기는 random2() 가 아닌 random2f() method 를사용한다.
```java
SinOsc s => dac;
for (0 => int count; count < 16; count ++){
    Math.random2(48,72) => int note;
    Math.random2f(0.1,0.9) => float volume;
    Std.mtof(note) => s.freq;
    volume => s.gain;
    0.3::second => now;
    
}
```

### s.freq , s.gain 콘솔출력
s는 객체이고 freq는 field variable 이다. 값을 가지고 올 수 있는 method 는 s.freq() 이다.
default 로 freq 는 220 gain은 1.0 으로 설정되어있다.

``` java
SinOsc s => dac;
220 => s.freq;
<<< s.freq() >>>;
```

``` java
SinOsc s => dac;
0.6 => s.gain;
<<< s.gain() >>>;
```



🌜 주관적인 견해가 담긴 글입니다. 다양한 의견이 있으실 경우
언제든지 댓글 혹은 메일로 지적해주시면 감사하겠습니다! 😄

